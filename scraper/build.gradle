plugins {
    id 'java-library'
    id 'org.jetbrains.kotlin.jvm'
    id 'kotlin-kapt'
    id("com.google.devtools.ksp").version("1.7.10-1.0.6")
    id 'org.jetbrains.dokka'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'

    // Shared module, including models shared across the main app and the scrapers
    implementation project(':shared')

    // Retrofit for doing HTTP requests much more easily
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'

    // Moshi, and associated retrofit adapters, so that we can easily convert JSON to Kotlin objects automatically
    implementation 'com.squareup.retrofit2:converter-moshi:2.9.0'
    implementation 'com.squareup.moshi:moshi-kotlin:1.13.0'
    kapt "com.squareup.moshi:moshi-kotlin-codegen:1.13.0"

    // Kotlin coroutines so we can correctly use suspend functions for tests
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4'

    // Jspoon, associated retrofit adapter, and Jsoup, for easy conversion of HTML to Java objects
    implementation('pl.droidsonroids:jspoon:1.3.2') {
        exclude group: 'org.jsoup', module: 'jsoup'
    }
    implementation 'org.jsoup:jsoup:1.15.2'
    implementation 'pl.droidsonroids.retrofit2:converter-jspoon:1.3.2'
}

test {
    useJUnitPlatform()

    filter {
        excludeTestsMatching("com.example.cosc345project.scrapertests.*")
    }
}

dokkaHtmlPartial {
    moduleName.set("Scrapers")

    dokkaSourceSets {
        configureEach {
            includes.from("Module.md")
        }
    }
}